%let path=/home/u59428818/EPG194/projectSAS;
libname tsa "/home/u59428818/EPG194/projectSAS";
options validvarname=v7;

proc import datafile="&path/TSAClaims2002_2017.csv" dbms=csv 
		out=tsa.claims_import replace;
	guessingrows=max;
run;

data tsa.claims_cleaned_no_missing;
	set tsa.claims_import;

	if Claim_Type in ("-", "") then
		Claim_Type="Unknown";
	else if Claim_Type='Passenger Property Loss/Personal Injur' then
		Claim_Type='Passenger Property Loss';
	else if Claim_Type='Passenger Property Loss/Personal Injury' then
		Claim_Type='Passenger Property Loss';
	else if Claim_Type='Property Damage/Personal Injury' then
		Claim_Type='Property Damage';

	if Claim_Site in ("-", " ") then
		Claim_Site='Unknown';

	if Disposition in ("-", "") then
		Disposition='Unknown';
	else if Disposition='Closed: Canceled' then
		Disposition='Closed:Canceled';
	else if Disposition='losed: Contractor Claim' then
		Disposition='Closed:Contractor Claim';
	State=upcase(state);
	StateName=propcase(StateName);

	if (Incident_Date > Date_Received or Incident_Date=. or Date_Received=. or 
		year(Incident_Date) < 2002 or year(Incident_Date) > 2017 or 
		year(Date_Received) < 2002 or year(Date_Received) > 2017) then
			Date_Issues="Needs Review";
	drop County city;
	format Incident_Date Date_Received date9. Close_Amount Dollar20.2;
	label Airport_Code="Airport Code" Airport_Name="Airport Name" 
		Claim_Number="Claim Number" Claim_Site="Claim Site" Claim_Type="Claim Type" 
		Close_Amount="Close Amount" Date_Issues="Date Issues" 
		Date_Received="Date Received" Incident_Date="Incident Date" 
		Item_Category="Item Category";
run;

proc sort data=tsa.claims_cleaned_no_missing nodupkey out=tsa.Claims_Cleaned 
		dupout=tsa.dup_data;
	by _all_;
run;

%let outpath=/home/u59428818/EPG194/projectSAS;
ods pdf file="&outpath/ClaimsReports.pdf" style=Meadow;
title "Overall Date Issues in the Data";

proc freq data=TSA.Claims_Cleaned;
	table Date_Issues / nocum nopercent;
run;

title "Overall Claims by Year";

proc freq data=TSA.Claims_Cleaned;
	table Incident_Date / nocum nopercent plots=freqplot;
	format Incident_Date year4.;
	where Date_Issues is null;
run;

%let StateName=California;
title "&StateName Claim Types, Claim Sites and Disposition Frequencies";

proc freq data=TSA.Claims_Cleaned order=freq;
	table Claim_Type Claim_Site Disposition / nocum nopercent;
	where StateName="&StateName" and Date_Issues is null;
run;

title "Close_Amount Statistics for &StateName";

proc means data=TSA.Claims_Cleaned mean min max sum maxdec=0;
	var Close_Amount;
	where StateName="&StateName" and Date_Issues is null;
run;

ods pdf close;